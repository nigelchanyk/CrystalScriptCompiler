import crystalscriptcompiler.syntaxtree.*;
import crystalscriptcompiler.syntaxtree.classes.*;
import crystalscriptcompiler.syntaxtree.expressions.*;
import crystalscriptcompiler.syntaxtree.expressions.basic.*;
import crystalscriptcompiler.syntaxtree.expressions.primary.*;
import crystalscriptcompiler.syntaxtree.expressions.primary.accesses.*;
import crystalscriptcompiler.syntaxtree.expressions.primary.literals.*;
import crystalscriptcompiler.syntaxtree.expressions.primary.methodinvocation.*;
import crystalscriptcompiler.syntaxtree.fields.*;
import crystalscriptcompiler.syntaxtree.imports.*;
import crystalscriptcompiler.syntaxtree.interfaces.*;
import crystalscriptcompiler.syntaxtree.methods.*;
import crystalscriptcompiler.syntaxtree.names.*;
import crystalscriptcompiler.syntaxtree.statements.*;
import crystalscriptcompiler.syntaxtree.statements.exceptions.*;
import crystalscriptcompiler.syntaxtree.statements.forloops.*;
import crystalscriptcompiler.syntaxtree.statements.switches.*;
import crystalscriptcompiler.syntaxtree.types.*;
import crystalscriptcompiler.syntaxtree.variables.*;
import crystalscriptcompiler.syntaxtree.variables.initializers.*;

parser code  {:

	private ParseTreeRoot root;
	
	public ParseTreeRoot getRoot() {
		return root;
	}

	public void setRoot(ParseTreeRoot root) {
		this.root = root;
	}

  public void report_error(String message, Object info) {
    StringBuffer m = new StringBuffer("Error ");

    if (info instanceof java_cup.runtime.Symbol) {
	  java_cup.runtime.Symbol sym = (java_cup.runtime.Symbol)info;
	  m.append( "At line " + sym.left + " character " + sym.right);
      m.append( "("+info.toString()+")" );
	}
     
    m.append(" : "+message);
   
    System.out.println(m);
  }
   
  public void report_fatal_error(String message, Object info) {
    report_error(message, info);
    throw new RuntimeException("Fatal Syntax Error");
  }
:};

terminal ARRAY, BOOLEAN, NUMBER, OBJECT, STRING, VAR; // types
terminal LBRACK, RBRACK; // array_type
terminal DOT; // qualified_name
terminal SEMICOLON, MULT, COMMA, EQ, LPAREN, RPAREN, LBRACE, RBRACE, COLON;
terminal IMPORT; // import_declaration
terminal AS;
terminal FROM;
terminal PUBLIC, PROTECTED, PRIVATE; // modifier
terminal STATIC; // modifier
terminal ABSTRACT;
terminal CLASS; // class_declaration
terminal EXTENDS; // super
terminal IMPLEMENTS; // interfaces
terminal CONSTRUCTOR;
terminal DEF; // method_declaration
terminal THIS, SUPER; // explicit_constructor_invocation
terminal INTERFACE; // interface_declaration
terminal IF, ELSE; // if_then_statement, if_then_else_statement
terminal SWITCH; // switch_statement
terminal CASE, DEFAULT; // switch_label
terminal DO, WHILE; // while_statement, do_statement
terminal FOR; // for_statement
terminal BREAK; // break_statement
terminal CONTINUE; // continue_statement
terminal RETURN; // return_statement
terminal THROW; // throw_statement
terminal TRY; // try_statement
terminal CATCH; // catch_clause
terminal FINALLY; // finally
terminal NEW; // class_instance_creation_expression
terminal PLUSPLUS; // postincrement_expression
terminal MINUSMINUS; // postdecrement_expression
terminal PLUS, MINUS, COMP, NOT, DIV, MOD;
terminal LSHIFT, RSHIFT, URSHIFT; // shift_expression
terminal LT, GT, LTEQ, GTEQ, IS, ISNT; // relational_expression
terminal EQEQ, NOTEQ; // equality_expression
terminal AND; // and_expression
terminal XOR; // exclusive_or_expression
terminal OR;  // inclusive_or_expression
terminal ANDAND; // conditional_and_expression
terminal OROR; // conditional_or_expression
terminal QUESTION; // conditional_expression
terminal MULTEQ, DIVEQ, MODEQ, PLUSEQ, MINUSEQ; // assignment_operator
terminal LSHIFTEQ, RSHIFTEQ, URSHIFTEQ; // assignment_operator
terminal ANDEQ, XOREQ, OREQ; // assignment_operator
terminal PASS;

terminal NUMBER_LITERAL;
terminal BOOLEAN_LITERAL;
terminal STRING_LITERAL;
terminal NULL_LITERAL;
terminal UNDEFINED_LITERAL;
terminal IDENTIFIER;

// 19.2) The Syntactic Grammar
non terminal goal;
non terminal top_level_sections_opt, top_level_sections, top_level_section;
// 19.3) Lexical Structure
non terminal literal;
// 19.4) Types, Values, and Variables
non terminal type, primitive_type;
non terminal reference_type;
non terminal class_or_interface_type;
non terminal class_type, interface_type;
// 19.5) Names
non terminal name, qualified_name;
// 19.6) Packages
non terminal import_declarations_opt, import_declarations;
non terminal import_declaration;
non terminal import_items, import_name;
// 19.7) Productions used only in the LALR(1) grammar
non terminal modifiers_opt, modifiers, modifier;
// 19.8.1) Class Declaration
non terminal class_declaration, super_cl, super_opt;
non terminal interfaces, interfaces_opt, interface_type_list;
non terminal class_body_declarations, class_body_declarations_opt;
non terminal class_body_declaration, class_member_declaration;
// 19.8.2) Field Declarations
non terminal field_declaration, variable_declarators, variable_declarator;
non terminal variable_declarator_id, variable_initializer;
// 19.8.3) Method Declarations
non terminal method_declaration, method_header, method_declarator;
non terminal formal_parameter_list_opt, formal_parameter_list;
non terminal formal_parameter;
// 19.8.5) Constructor Declarations
non terminal constructor_declaration, constructor_declarator;
non terminal explicit_constructor_invocation_opt, explicit_constructor_invocation;
// 19.9.1) Interface Declarations
non terminal interface_declaration;
non terminal extends_interfaces_opt, extends_interfaces;
non terminal interface_member_declarations_opt, interface_member_declarations;
non terminal interface_member_declaration, constant_declaration;
non terminal abstract_method_declaration;
// 19.10) Arrays
non terminal array_initializer;
non terminal variable_initializers;
// 19.11) Blocks and Statements
non terminal block;
non terminal block_statements_opt, block_statements, block_statement;
non terminal local_variable_declaration_statement, local_variable_declaration;
non terminal statement, statement_no_short_if;
non terminal statement_without_trailing_substatement;
non terminal empty_statement;
non terminal labeled_statement, labeled_statement_no_short_if;
non terminal expression_statement, statement_expression;
non terminal if_then_statement;
non terminal if_then_else_statement, if_then_else_statement_no_short_if;
non terminal switch_statement;
non terminal switch_block_statement_groups_opt;
non terminal switch_block_statement_groups;
non terminal switch_block_statement_group;
non terminal switch_case_header;
non terminal switch_labels, switch_label;
non terminal while_statement, while_statement_no_short_if;
non terminal do_statement;
non terminal for_statement, for_statement_no_short_if;
non terminal for_init_opt, for_init;
non terminal for_update_opt, for_update;
non terminal statement_expression_list;
non terminal break_statement, continue_statement;
non terminal return_statement, throw_statement;
non terminal try_statement;
non terminal catches, catch_clause;
non terminal finally;
// 19.12) Expressions
non terminal primary, primary_no_new_array;
non terminal class_instance_creation_expression;
non terminal argument_list_opt, argument_list;
non terminal array_creation_expression, object_creation_expression;
non terminal field_access, method_invocation, array_access;
non terminal postfix_expression;
non terminal postincrement_expression, postdecrement_expression;
non terminal unary_expression, unary_expression_not_plus_minus;
non terminal preincrement_expression, predecrement_expression;
non terminal multiplicative_expression, additive_expression;
non terminal shift_expression, relational_expression, equality_expression;
non terminal and_expression, exclusive_or_expression, inclusive_or_expression;
non terminal conditional_and_expression, conditional_or_expression;
non terminal conditional_expression, assignment_expression;
non terminal assignment;
non terminal left_hand_side;
non terminal assignment_operator;
non terminal expression_opt, expression;
non terminal constant_expression;

start with goal;

// 19.2) The Syntactic Grammar
goal ::= import_declarations_opt:i top_level_sections_opt:t
	{: parser.setRoot(new ParseTreeRoot((ImportDeclarations)i, (TopLevelSections)t)); :}
	;
top_level_sections_opt ::= {: RESULT = new TopLevelSections(); :}
	| top_level_sections:t {: RESULT = t; :}
	;
top_level_sections ::= top_level_section:t {: RESULT = new TopLevelSections((TopLevelSection)t); :}
	| top_level_section:t top_level_sections:u {: RESULT = new TopLevelSections((TopLevelSection)t, (TopLevelSections)u); :}
	;
top_level_section ::= class_declaration:c {: RESULT = new TopLevelSection((ClassDeclaration)c); :}
	|	interface_declaration:i {: RESULT = new TopLevelSection((InterfaceDeclaration)i); :}
	;

// 19.3) Lexical Structure.
literal ::=	NUMBER_LITERAL:n {: RESULT = new NumberLiteral((Double)n); :}
	|	BOOLEAN_LITERAL:b {: RESULT = new BooleanLiteral((Boolean)b); :}
	|	STRING_LITERAL:s {: RESULT = new StringLiteral((String)s); :}
	|	NULL_LITERAL {: RESULT = new NullLiteral(); :}
	|	UNDEFINED_LITERAL {: RESULT = new UndefinedLiteral(); :}
	;

// 19.4) Types, Values, and Variables
type	::=	primitive_type:p {: RESULT = p; :}
	|	reference_type:r {: RESULT = r; :}
	|	VAR {: RESULT = new VarType(); :}
	;
primitive_type ::= NUMBER {: RESULT = new PrimitiveType(PrimitiveType.Kind.NUMBER); :}
	|	STRING {: RESULT = new PrimitiveType(PrimitiveType.Kind.STRING); :}
	|	BOOLEAN {: RESULT = new PrimitiveType(PrimitiveType.Kind.BOOLEAN); :}
	|	OBJECT {: RESULT = new PrimitiveType(PrimitiveType.Kind.OBJECT); :}
	|	ARRAY {: RESULT = new PrimitiveType(PrimitiveType.Kind.ARRAY); :}
	;
reference_type ::=
		class_or_interface_type:t {: RESULT = t; :}
	;
	
class_or_interface_type ::= name:n {: RESULT = new ClassOrInterfaceType((Name)n); :}
	;
class_type ::=	class_or_interface_type:t {: RESULT = t; :}
	;
interface_type ::= class_or_interface_type:t {: RESULT = t; :}
	;

// 19.5) Names
name ::= IDENTIFIER:i {: RESULT = new Name((String)i); :}
	|	qualified_name:q {: RESULT = q; :}
	;
qualified_name ::=
		name:n DOT IDENTIFIER:i {: RESULT = new QualifiedName((Name)n, (String)i); :}
	;

// 19.6) Packages
import_declarations_opt ::= {: RESULT = new ImportDeclarations(); :}
	| import_declarations:i {: RESULT = i; :}
	;
import_declarations ::= 
		import_declaration:i {: RESULT = new ImportDeclarations((ImportDeclaration)i); :}
	|	import_declarations:j import_declaration:i {: RESULT = new ImportDeclarations((ImportDeclaration)i, (ImportDeclarations)j, SequentialCollection.Order.REVERSE); :}
	;
import_declaration ::= 
		IMPORT import_name:n SEMICOLON {: RESULT = new ImportDeclaration((ImportName)n); :}
	|	FROM name:n IMPORT import_items:i SEMICOLON {: RESULT = new ImportDeclaration(new ImportName((Name)n), (ImportItems)i); :}
	;
	
import_items ::=
		import_name:n {: RESULT = new ImportItems((ImportName)n); :}
	|	import_name:n COMMA import_items:i {: RESULT = new ImportItems((ImportName)n, (ImportItems)i); :}
	;
	
import_name ::=
		name:n {: RESULT = new ImportName((Name)n); :}
	|	name:n AS IDENTIFIER:i {: RESULT = new ImportName((Name)n, (String)i); :}
	;

// 19.7) Productions used only in the LALR(1) grammar
modifiers_opt ::= {: RESULT = new Modifiers(); :}
	|	modifiers:m {: RESULT = m; :}
	;
modifiers ::= modifier:m {: RESULT = new Modifiers((Modifier)m); :}
	|	modifiers:n modifier:m {: RESULT = new Modifiers((Modifier)m, (Modifiers)n); :}
	;
modifier ::= PUBLIC {: RESULT = Modifier.PUBLIC; :}
	| PROTECTED {: RESULT = Modifier.PROTECTED; :}
	| PRIVATE {: RESULT = Modifier.PRIVATE; :}
	| STATIC {: RESULT = Modifier.STATIC; :}
	| ABSTRACT {: RESULT = Modifier.ABSTRACT; :}
	;

// 19.8) Classes

// 19.8.1) Class Declaration:
class_declaration ::= 
	modifiers_opt:m CLASS IDENTIFIER:n super_opt:s interfaces_opt:i LBRACE class_body_declarations_opt:b RBRACE
	{: RESULT = new ClassDeclaration((Modifiers)m, (String)n, (SuperclassOptional)s, (Interfaces)i, (MemberDeclarations)b); :}
	;
super_cl ::=	EXTENDS class_type:t {: RESULT = t; :}
	;
super_opt ::=	{: RESULT = new SuperclassOptional(); :}
	|	super_cl:s {: RESULT = new SuperclassOptional((ClassOrInterfaceType)s); :}
	;
interfaces ::=	IMPLEMENTS interface_type_list:i {: RESULT = i; :}
	;
interfaces_opt::= {: RESULT = new Interfaces(); :}
	|	interfaces:i {: RESULT = i; :}
	;
interface_type_list ::= 
		interface_type:t {: RESULT = new Interfaces((ClassOrInterfaceType)t); :}
	|	interface_type_list:u COMMA interface_type:t {: RESULT = new Interfaces((ClassOrInterfaceType)t, (Interfaces)u, SequentialCollection.Order.REVERSE); :}
	;
class_body_declarations_opt ::= {: RESULT = new MemberDeclarations(); :}
	|	class_body_declarations:b {: RESULT = b; :}
	;
class_body_declarations ::= 
		class_body_declaration:b {: RESULT = new MemberDeclarations((MemberDeclaration)b); :}
	|	class_body_declarations:c class_body_declaration:b {: RESULT = b instanceof MemberDeclaration ? new MemberDeclarations((MemberDeclaration)b, (MemberDeclarations)c) : new MemberDeclarations((MemberDeclarations)b, (MemberDeclarations)c); :}
	;
class_body_declaration ::=
	class_member_declaration:m {: RESULT = m; :}
	|	constructor_declaration:c {: RESULT = c; :}
	;
class_member_declaration ::=
		field_declaration:f {: RESULT = f; :}
	|	method_declaration:m {: RESULT = m; :}
	|	class_declaration:c {: RESULT = c; :}
	|	interface_declaration:i {: RESULT = i; :}
	;

// 19.8.2) Field Declarations
field_declaration ::= 
		modifiers_opt:m type:t variable_declarators:d SEMICOLON {: RESULT = new FieldDeclarations((Modifiers)m, (Type)t, (VariableDeclarators)d); :}
	;
variable_declarators ::=
		variable_declarator:d {: RESULT = new VariableDeclarators((VariableDeclarator)d); :}
	|	variable_declarators:e COMMA variable_declarator:d {: RESULT = new VariableDeclarators((VariableDeclarator)d, (VariableDeclarators)e, SequentialCollection.Order.REVERSE); :}
	;
variable_declarator ::=
		variable_declarator_id:v {: RESULT = new VariableDeclarator((String)v); :}
	|	variable_declarator_id:v EQ variable_initializer:i {: RESULT = new VariableDeclarator((String)v, (GenericVariableInitializer)i); :}
	;
variable_declarator_id ::=
		IDENTIFIER:i {: RESULT = i; :}
	;
variable_initializer ::=
		expression:e {: RESULT = new VariableInitializer((Expression)e); :}
	|	array_initializer:i {: RESULT = i; :}
	;

// 19.8.3) Method Declarations
method_declaration ::=
		method_header:h block:b {: RESULT = new MethodDeclaration((MethodHeader)h, (Block)b); :}
	;
method_header ::=
		modifiers_opt:m type:t method_declarator:d {: RESULT = new MethodHeader((Modifiers)m, (MethodDeclarator)d, (Type)t); :}
	|	modifiers_opt:m DEF method_declarator:d {: RESULT = new MethodHeader((Modifiers)m, (MethodDeclarator)d); :}
	;
method_declarator ::=
		IDENTIFIER:i LPAREN formal_parameter_list_opt:p RPAREN {: RESULT = new MethodDeclarator((String)i, (Parameters)p); :}
	;
formal_parameter_list_opt ::= {: RESULT = new Parameters(); :}
	|	formal_parameter_list:p {: RESULT = p; :}
	;
formal_parameter_list ::=
		formal_parameter:p {: RESULT = new Parameters((Parameter)p); :}
	|	formal_parameter_list:q COMMA formal_parameter:p {: RESULT = new Parameters((Parameter)p, (Parameters)q, SequentialCollection.Order.REVERSE); :}
	;
formal_parameter ::=
		type:t variable_declarator:v {: RESULT = new Parameter((VariableDeclarator)v, (Type)t); :}
	|	variable_declarator:v {: RESULT = new Parameter((VariableDeclarator)v); :}
	;

// 19.8.5) Constructor Declarations
constructor_declaration ::=
		modifiers_opt:m constructor_declarator:d block:b
		{: RESULT = new ConstructorDeclaration((Modifiers)m, (ConstructorDeclarator)d, (Block)b); :}
	;
constructor_declarator ::=
		CONSTRUCTOR LPAREN formal_parameter_list_opt:p RPAREN explicit_constructor_invocation_opt:e
		{: RESULT = new ConstructorDeclarator((Parameters)p, (ConstructorInvocationOptional)e); :}
	;
explicit_constructor_invocation_opt ::= {: RESULT = new ConstructorInvocationOptional(); :}
	|	COLON explicit_constructor_invocation:e {: RESULT = new ConstructorInvocationOptional((ConstructorInvocation)e); :}
	;

explicit_constructor_invocation ::=
		THIS LPAREN argument_list_opt:a RPAREN
		{: RESULT = new ConstructorInvocation(ConstructorInvocation.Kind.THIS, (ArgumentList)a); :}
	|	SUPER LPAREN argument_list_opt:a RPAREN
		{: RESULT = new ConstructorInvocation(ConstructorInvocation.Kind.SUPER, (ArgumentList)a); :}
	;

// 19.9) Interfaces

// 19.9.1) Interface Declarations
interface_declaration ::=
		modifiers_opt:m INTERFACE IDENTIFIER:i extends_interfaces_opt:e LBRACE interface_member_declarations_opt:b RBRACE
		{: RESULT = new InterfaceDeclaration((Modifiers)m, (String)i, (Interfaces)e, (MemberDeclarations)b); :}
	;
extends_interfaces_opt ::= {: RESULT = new Interfaces(); :}
	|	EXTENDS extends_interfaces:i {: RESULT = i; :}
	;
extends_interfaces ::=
		interface_type:i {: RESULT = new Interfaces((ClassOrInterfaceType)i); :}
	|	extends_interfaces:j COMMA interface_type:i {: RESULT = new Interfaces((ClassOrInterfaceType)i, (Interfaces)j, SequentialCollection.Order.REVERSE); :}
	;
interface_member_declarations_opt ::= {: RESULT = new MemberDeclarations(); :}
	|	interface_member_declarations:d {: RESULT = d; :}
	;
interface_member_declarations ::=
		interface_member_declaration:m {: RESULT = new MemberDeclarations((MemberDeclaration)m); :}
	|	interface_member_declarations:n interface_member_declaration:m {: RESULT = m instanceof MemberDeclaration ? new MemberDeclarations((MemberDeclaration)m, (MemberDeclarations)n, SequentialCollection.Order.REVERSE) : new MemberDeclarations((MemberDeclarations)m, (MemberDeclarations)n, SequentialCollection.Order.REVERSE); :}
	;
interface_member_declaration ::=
		constant_declaration:c {: RESULT = c; :}
	|	abstract_method_declaration:a {: RESULT = a; :}
	|	class_declaration:c {: RESULT = c; :}
	|	interface_declaration:i {: RESULT = i; :}
	;
constant_declaration ::=
		field_declaration:f {: RESULT = f; :}
	;
abstract_method_declaration ::=
		method_header:h SEMICOLON {: RESULT = new MethodDeclaration((MethodHeader)h); :}
	;

// 19.10) Arrays
array_initializer ::=
		LBRACK variable_initializers:i COMMA RBRACK {: RESULT = new ArrayInitializer((GenericVariableInitializers)i); :}
	|	LBRACK variable_initializers:i RBRACK {: RESULT = new ArrayInitializer((GenericVariableInitializers)i); :}
	|	LBRACK COMMA RBRACK {: RESULT = new ArrayInitializer(); :}
	|	LBRACK RBRACK {: RESULT = new ArrayInitializer(); :}
	;
variable_initializers ::=
		variable_initializer:i {: RESULT = new GenericVariableInitializers((GenericVariableInitializer)i); :}
	|	variable_initializers:j COMMA variable_initializer:i {: RESULT = new GenericVariableInitializers((GenericVariableInitializer)i, (GenericVariableInitializers)j); :}
	;

// 19.11) Blocks and Statements
block ::=	LBRACE block_statements_opt:b RBRACE {: RESULT = new Block((BlockStatements)b); :}
	;
block_statements_opt ::= {: RESULT = new BlockStatements(); :}
	|	block_statements:s {: RESULT = s; :}
	;
block_statements ::=
		block_statement:b {: RESULT = new BlockStatements((BlockStatement)b); :}
	|	block_statements:c block_statement:b {: RESULT = new BlockStatements((BlockStatement)b, (BlockStatements)c, SequentialCollection.Order.REVERSE); :}
	;
block_statement ::=
		local_variable_declaration_statement:d {: RESULT = d; :}
	|	statement:s {: RESULT = s; :}
	|	class_declaration:c {: RESULT = c; :}
	|	interface_declaration:i {: RESULT = i; :}
	;
local_variable_declaration_statement ::=
		local_variable_declaration:d SEMICOLON {: RESULT = new DeclarationStatement((LocalVariableDeclaration)d); :}
	;
local_variable_declaration ::=
		type:t variable_declarators:d {: RESULT = new LocalVariableDeclaration((Type)t, (VariableDeclarators)d); :}
	;
statement ::=	statement_without_trailing_substatement:s {: RESULT = s; :}
	|	labeled_statement:l {: RESULT = l; :}
	|	if_then_statement:i {: RESULT = i; :}
	|	if_then_else_statement:i {: RESULT = i; :}
	|	while_statement:w {: RESULT = w; :}
	|	for_statement:f {: RESULT = f; :}
	;
statement_no_short_if ::=	statement_without_trailing_substatement:s {: RESULT = s; :}
	|	labeled_statement_no_short_if:l {: RESULT = l; :}
	|	if_then_else_statement_no_short_if:i {: RESULT = i; :}
	|	while_statement_no_short_if:w {: RESULT = w; :}
	|	for_statement_no_short_if:f {: RESULT = f; :}
	;
statement_without_trailing_substatement ::=
		block:b {: RESULT = b; :}
	|	empty_statement:e {: RESULT = e; :}
	|	expression_statement:e {: RESULT = e; :}
	|	switch_statement:s {: RESULT = s; :}
	|	do_statement:d {: RESULT = d; :}
	|	break_statement:b {: RESULT = b; :}
	|	continue_statement:c {: RESULT = c; :}
	|	return_statement:r {: RESULT = r; :}
	|	throw_statement:t {: RESULT = t; :}
	|	try_statement:t {: RESULT = t; :}
	;
empty_statement ::=
		PASS SEMICOLON {: RESULT = new EmptyStatement(); :}
	;
labeled_statement ::=
		IDENTIFIER:i COLON statement:s {: RESULT = new LabeledStatement((String)i, (BlockStatement)s); :}
	;
expression_statement ::=
		statement_expression:s SEMICOLON {: RESULT = new ExpressionStatement((StatementExpression)s); :}
	;
statement_expression ::=
		assignment:a {: RESULT = a; :}
	|	preincrement_expression:p {: RESULT = p; :}
	|	predecrement_expression:p {: RESULT = p; :}
	|	postincrement_expression:p {: RESULT = p; :}
	|	postdecrement_expression:p {: RESULT = p; :}
	|	method_invocation:m {: RESULT = m; :}
	|	class_instance_creation_expression:c {: RESULT = c; :}
	;
if_then_statement ::=
		IF LPAREN expression:e RPAREN statement:s {: RESULT = new IfStatement((Expression)e, (BlockStatement)s); :}
	;
if_then_else_statement ::=
		IF LPAREN expression:e RPAREN statement_no_short_if:s
			ELSE statement:t
		{: RESULT = new IfStatement((Expression)e, (BlockStatement)s, (BlockStatement)t); :}
	;
if_then_else_statement_no_short_if ::=
		IF LPAREN expression:e RPAREN statement_no_short_if:s
			ELSE statement_no_short_if:t
		{: RESULT = new IfStatement((Expression)e, (BlockStatement)s, (BlockStatement)t); :}
	;

switch_statement ::=
		SWITCH LPAREN expression:e RPAREN LBRACE switch_block_statement_groups_opt:g RBRACE {: RESULT = new SwitchStatement((Expression)e, (SwitchGroups)g); :}
	;
switch_block_statement_groups_opt ::= {: RESULT = new SwitchGroups(); :}
	|	switch_block_statement_groups:g {: RESULT = g; :}
	;
switch_block_statement_groups ::=
		switch_block_statement_group:g {: RESULT = new SwitchGroups((SwitchGroup)g); :}
	|	switch_block_statement_groups:h switch_block_statement_group:g {: RESULT = new SwitchGroups((SwitchGroup)g, (SwitchGroups)h, SequentialCollection.Order.REVERSE); :}
	;
switch_block_statement_group ::=
		switch_case_header:h block_statements:s {: RESULT = new SwitchGroup((SwitchLabels)h, (BlockStatements)s); :}
	;
switch_case_header ::=
		CASE switch_labels:l COLON {: RESULT = l; :}
	|	DEFAULT COLON {: RESULT = new SwitchLabels(); :}
	;
switch_labels ::=
		switch_label:l {: RESULT = new SwitchLabels((SwitchLabel)l); :}
	|	switch_labels:m COMMA switch_label:l {: RESULT = new SwitchLabels((SwitchLabel)l, (SwitchLabels)m, SequentialCollection.Order.REVERSE); :}
	;
switch_label ::= constant_expression:e {: RESULT = new SwitchLabel((Expression)e); :}
	;
while_statement ::=
		WHILE LPAREN expression:e RPAREN statement:s {: RESULT = new WhileStatement((Expression)e, (BlockStatement)s, WhileStatement.ConditionOrder.BEFORE); :}
	;
while_statement_no_short_if ::=
		WHILE LPAREN expression:e RPAREN statement_no_short_if:s
		{: RESULT = new WhileStatement((Expression)e, (BlockStatement)s, WhileStatement.ConditionOrder.BEFORE); :}
	;
do_statement ::=
		DO statement:s WHILE LPAREN expression:e RPAREN SEMICOLON {: RESULT = new WhileStatement((Expression)e, (BlockStatement)s, WhileStatement.ConditionOrder.AFTER); :}
	;
for_statement ::=
		FOR LPAREN for_init_opt:f SEMICOLON expression_opt:e SEMICOLON for_update_opt:u RPAREN statement:s
		{: RESULT = new ForStatement((ForInitializer)f, (Expression)e, (StatementExpressionList)u, (BlockStatement)s); :}
	;
for_statement_no_short_if ::=
		FOR LPAREN for_init_opt:f SEMICOLON expression_opt:e SEMICOLON for_update_opt:u RPAREN statement_no_short_if:s
		{: RESULT = new ForStatement((ForInitializer)f, (Expression)e, (StatementExpressionList)u, (BlockStatement)s); :}
	;
for_init_opt ::= {: RESULT = new ForInitializer(); :}
	|	for_init:i {: RESULT = i; :}
	;
for_init ::= statement_expression_list:e {: RESULT = new ForInitializer((StatementExpressionList)e); :}
	|	local_variable_declaration:d {: RESULT = new ForInitializer((LocalVariableDeclaration)d); :}
	;
for_update_opt ::= {: RESULT = new StatementExpressionList(); :}
	|	for_update:u {: RESULT = u; :}
	;
for_update ::= statement_expression_list:e {: RESULT = e; :}
	;
statement_expression_list ::=
		statement_expression:e {: RESULT = new StatementExpressionList((StatementExpression)e); :}
	|	statement_expression_list:f COMMA statement_expression:e {: RESULT = new StatementExpressionList((StatementExpression)e, (StatementExpressionList)f, SequentialCollection.Order.REVERSE); :}
	;

break_statement ::=
		BREAK SEMICOLON {: RESULT = new ControlStatement(ControlStatement.Kind.BREAK); :}
	|	BREAK IDENTIFIER:i SEMICOLON {: RESULT = new ControlStatement(ControlStatement.Kind.BREAK, (String)i); :}
	;

continue_statement ::=
		CONTINUE SEMICOLON {: RESULT = new ControlStatement(ControlStatement.Kind.CONTINUE); :}
	|	CONTINUE IDENTIFIER:i SEMICOLON {: RESULT = new ControlStatement(ControlStatement.Kind.CONTINUE, (String)i); :}
	;
return_statement ::=
		RETURN SEMICOLON {: RESULT = new ReturnStatement(); :}
	|	RETURN expression:e SEMICOLON {: RESULT = new ReturnStatement((Expression)e); :}
	;
throw_statement ::=
		THROW expression:e SEMICOLON {: RESULT = new ThrowStatement((Expression)e); :}
	;
try_statement ::=
		TRY block:s catches:c {: RESULT = new TryStatement((Block)s, (CatchStatements)c); :}
	|	TRY block:s catches:c finally:f {: RESULT = new TryStatement((Block)s, (CatchStatements)c, (Block)f); :}
	|	TRY block:s finally:f {: RESULT = new TryStatement((Block)s, (Block)f); :}
	;
catches ::=	catch_clause:c {: RESULT = new CatchStatements((CatchStatement)c); :}
	|	catches:d catch_clause:c {: RESULT = new CatchStatements((CatchStatement)c, (CatchStatements)d, SequentialCollection.Order.REVERSE); :}
	;
catch_clause ::=
		CATCH LPAREN formal_parameter:p RPAREN block:b {: RESULT = new CatchStatement((Parameter)p, (Block)b); :}
	;
finally ::=	FINALLY block:b {: RESULT = b; :}
	;

// 19.12) Expressions
primary ::=	primary_no_new_array:p {: RESULT = p; :}
	|	array_creation_expression:c {: RESULT = c; :}
	|	object_creation_expression:c {: RESULT = c; :}
	;
primary_no_new_array ::=
		literal:l {: RESULT = l; :}
	|	THIS {: RESULT = new ThisPrimary(); :}
	|	LPAREN expression:e RPAREN {: RESULT = e; :}
	|	class_instance_creation_expression:e {: RESULT = e; :}
	|	field_access:a {: RESULT = a; :}
	|	method_invocation:m {: RESULT = m; :}
	|	array_access:a {: RESULT = a; :}
	;
class_instance_creation_expression ::=
		NEW class_type:c LPAREN argument_list_opt:a RPAREN {: RESULT = new InstantiationExpression((ClassOrInterfaceType)c, (ArgumentList)a); :}
	|	NEW class_type:t LPAREN argument_list_opt:a RPAREN LBRACE class_body_declarations_opt:d RBRACE
		{: RESULT = new InstantiationExpression((ClassOrInterfaceType)t, (ArgumentList)a, (MemberDeclarations)d); :}
	;
argument_list_opt ::= {: RESULT = new ArgumentList(); :}
	|	argument_list:a {: RESULT = a; :}
	;
argument_list ::=
		expression:e {: RESULT = new ArgumentList((Expression)e); :}
	|	argument_list:a COMMA expression:e {: RESULT = new ArgumentList((Expression)e, (ArgumentList)a, SequentialCollection.Order.REVERSE); :}
	;
array_creation_expression ::=
		NEW ARRAY LPAREN argument_list_opt:a RPAREN {: RESULT = new InstantiationExpression(new PrimitiveType(PrimitiveType.Kind.ARRAY), (ArgumentList)a); :}
	;
object_creation_expression ::=
		NEW OBJECT LPAREN argument_list_opt:a RPAREN {: RESULT = new InstantiationExpression(new PrimitiveType(PrimitiveType.Kind.OBJECT), (ArgumentList)a); :}
	;
field_access ::= primary:p DOT IDENTIFIER:i {: RESULT = new NonInheritedFieldAccess((Primary)p, (String)i); :}
	|	SUPER DOT IDENTIFIER:i {: RESULT = new SuperFieldAccess((String)i); :}
	|	name:n DOT SUPER DOT IDENTIFIER:i {: RESULT = new SuperFieldAccess((String)i, (Name)n); :}
	;
method_invocation ::=
		name:n LPAREN argument_list_opt:a RPAREN {: RESULT = new ImplicitMethodInvocation((Name)n, (ArgumentList)a); :}
	|	primary:p DOT IDENTIFIER:i LPAREN argument_list_opt:a RPAREN {: RESULT = new NonInheritedMethodInvocation((Primary)p, (String)i, (ArgumentList)a); :}
	|	SUPER DOT IDENTIFIER:i LPAREN argument_list_opt:a RPAREN {: RESULT = new SuperMethodInvocation((String)i, (ArgumentList)a); :}
	|	name:n DOT SUPER DOT IDENTIFIER:i LPAREN argument_list_opt:a RPAREN {: RESULT = new SuperMethodInvocation((Name)n, (String)i, (ArgumentList)a); :}
	;
array_access ::=
		name:n LBRACK expression:e RBRACK {: RESULT = new ImplicitArrayAccess((Name)n, (Expression)e); :}
	|	primary_no_new_array:p LBRACK expression:e RBRACK {: RESULT = new PrimaryArrayAccess((Primary)p, (Expression)e); :}
	;
postfix_expression ::=
		primary:p {: RESULT = new PrimaryExpression((Primary)p); :}
	|	name:n {: RESULT = new NameExpression((Name)n); :}
	|	postincrement_expression:p {: RESULT = p; :}
	|	postdecrement_expression:p {: RESULT = p; :}
	;
postincrement_expression ::=
		postfix_expression:p PLUSPLUS {: RESULT = new UnaryExpression((UnaryExpression)p, UnaryExpression.Kind.POSTINCREMENT); :}
	;
postdecrement_expression ::=
		postfix_expression:p MINUSMINUS {: RESULT = new UnaryExpression((UnaryExpression)p, UnaryExpression.Kind.POSTDECREMENT); :}
	;
unary_expression ::=
		preincrement_expression:p {: RESULT = p; :}
	|	predecrement_expression:p {: RESULT = p; :}
	|	PLUS unary_expression:u {: RESULT = new UnaryExpression((UnaryExpression)u, UnaryExpression.Kind.PLUS); :}
	|	MINUS unary_expression:u {: RESULT = new UnaryExpression((UnaryExpression)u, UnaryExpression.Kind.MINUS); :}
	|	unary_expression_not_plus_minus:u {: RESULT = u; :}
	;
preincrement_expression ::=
		PLUSPLUS unary_expression:u {: RESULT = new UnaryExpression((UnaryExpression)u, UnaryExpression.Kind.PREINCREMENT); :}
	;
predecrement_expression ::=
		MINUSMINUS unary_expression:u {: RESULT = new UnaryExpression((UnaryExpression)u, UnaryExpression.Kind.PREDECREMENT); :}
	;
unary_expression_not_plus_minus ::=
		postfix_expression:p {: RESULT = p; :}
	|	COMP unary_expression:c {: RESULT = new UnaryExpression((UnaryExpression)c, UnaryExpression.Kind.COMPLEMENT); :}
	|	NOT unary_expression:n {: RESULT = new UnaryExpression((UnaryExpression)n, UnaryExpression.Kind.NEGATE); :}
	;
multiplicative_expression ::=
		unary_expression:u {: RESULT = u; :}
	|	multiplicative_expression:l MULT unary_expression:r {: RESULT = new ArithmeticExpression((BasicExpression)l, (BasicExpression)r, ArithmeticExpression.Kind.MULTIPLY); :}
	|	multiplicative_expression:l DIV unary_expression:r {: RESULT = new ArithmeticExpression((BasicExpression)l, (BasicExpression)r, ArithmeticExpression.Kind.DIVIDE); :}
	|	multiplicative_expression:l MOD unary_expression:r {: RESULT = new ArithmeticExpression((BasicExpression)l, (BasicExpression)r, ArithmeticExpression.Kind.MODULO); :}
	;
additive_expression ::=
		multiplicative_expression:m {: RESULT = m; :}
	|	additive_expression:a PLUS multiplicative_expression:m {: RESULT = new ArithmeticExpression((BasicExpression)a, (BasicExpression)m, ArithmeticExpression.Kind.ADD); :}
	|	additive_expression:a MINUS multiplicative_expression:m {: RESULT = new ArithmeticExpression((BasicExpression)a, (BasicExpression)m, ArithmeticExpression.Kind.MINUS); :}
	;
shift_expression ::=
		additive_expression:a {: RESULT = a; :}
	|	shift_expression:s LSHIFT additive_expression:a {: RESULT = new ArithmeticExpression((BasicExpression)s, (BasicExpression)a, ArithmeticExpression.Kind.LEFT_SHIFT); :}
	|	shift_expression:s RSHIFT additive_expression:a {: RESULT = new ArithmeticExpression((BasicExpression)s, (BasicExpression)a, ArithmeticExpression.Kind.RIGHT_SHIFT); :}
	|	shift_expression:s URSHIFT additive_expression:a {: RESULT = new ArithmeticExpression((BasicExpression)s, (BasicExpression)a, ArithmeticExpression.Kind.UNSIGNED_RIGHT_SHIFT); :}
	;
relational_expression ::=
		shift_expression:s {: RESULT = s; :}
	|	relational_expression:r LT shift_expression:s {: RESULT = new ComparativeExpression((BasicExpression)r, (BasicExpression)s, ComparativeExpression.Kind.LESS_THAN); :}
	|	relational_expression:r GT shift_expression:s {: RESULT = new ComparativeExpression((BasicExpression)r, (BasicExpression)s, ComparativeExpression.Kind.GREATER_THAN); :}
	|	relational_expression:r LTEQ shift_expression:s {: RESULT = new ComparativeExpression((BasicExpression)r, (BasicExpression)s, ComparativeExpression.Kind.LESS_THAN_EQUAL); :}
	|	relational_expression:r GTEQ shift_expression:s {: RESULT = new ComparativeExpression((BasicExpression)r, (BasicExpression)s, ComparativeExpression.Kind.GREATER_THAN_EQUAL); :}
	;
equality_expression ::=
		relational_expression:r {: RESULT = r; :}
	|	equality_expression:e EQEQ relational_expression:r {: RESULT = new EqualityExpression((BasicExpression)e, (BasicExpression)r, EqualityExpression.Kind.EQUAL); :}
	|	equality_expression:e NOTEQ relational_expression:r {: RESULT = new EqualityExpression((BasicExpression)e, (BasicExpression)r, EqualityExpression.Kind.NOT_EQUAL); :}
	|	equality_expression:r IS relational_expression:s {: RESULT = new EqualityExpression((BasicExpression)r, (BasicExpression)s, EqualityExpression.Kind.IS); :}
	|	equality_expression:r ISNT relational_expression:s {: RESULT = new EqualityExpression((BasicExpression)r, (BasicExpression)s, EqualityExpression.Kind.ISNT); :}
	;
and_expression ::=
		equality_expression:e {: RESULT = e; :}
	|	and_expression:a AND equality_expression:e {: RESULT = new BitwiseExpression((BasicExpression)a, (BasicExpression)e, BitwiseExpression.Kind.AND); :}
	;
exclusive_or_expression ::=
		and_expression:e {: RESULT = e; :}
	|	exclusive_or_expression:e XOR and_expression:a {: RESULT = new BitwiseExpression((BasicExpression)e, (BasicExpression)a, BitwiseExpression.Kind.XOR); :}
	;
inclusive_or_expression ::=
		exclusive_or_expression:e {: RESULT = e; :}
	|	inclusive_or_expression:o OR exclusive_or_expression:e {: RESULT = new BitwiseExpression((BasicExpression)o, (BasicExpression)e, BitwiseExpression.Kind.OR); :}
	;
conditional_and_expression ::=
		inclusive_or_expression:o {: RESULT = o; :}
	|	conditional_and_expression:a ANDAND inclusive_or_expression:o {: RESULT = new BinaryExpression((BasicExpression)a, (BasicExpression)o, BinaryExpression.Kind.AND); :}
	;
conditional_or_expression ::=
		conditional_and_expression:a {: RESULT = a; :}
	|	conditional_or_expression:o OROR conditional_and_expression:a {: RESULT = new BinaryExpression((BasicExpression)o, (BasicExpression)a, BinaryExpression.Kind.OR); :}
	;
conditional_expression ::=
		conditional_or_expression:o {: RESULT = o; :}
	|	conditional_or_expression:o QUESTION expression:e COLON conditional_expression:c
		{: RESULT = new TernaryExpression((BasicExpression)o, (Expression)e, (BasicExpression)c); :}
	;
assignment_expression ::=
		conditional_expression:e {: RESULT = e; :}
	|	assignment:a {: RESULT = a; :}
	;
assignment ::=	left_hand_side:l assignment_operator:o assignment_expression:e {: RESULT = new Assignment((LeftValue)l, (Assignment.Operator)o, (Expression)e); :}
	;
left_hand_side ::=
		name:n {: RESULT = n; :}
	|	field_access:f {: RESULT = f; :}
	|	array_access:a {: RESULT = a; :}
	;
assignment_operator ::=
		EQ {: RESULT = Assignment.Operator.EQ; :}
	|	MULTEQ {: RESULT = Assignment.Operator.MULTEQ; :}
	|	DIVEQ {: RESULT = Assignment.Operator.DIVEQ; :}
	|	MODEQ {: RESULT = Assignment.Operator.MODEQ; :}
	|	PLUSEQ {: RESULT = Assignment.Operator.PLUSEQ; :}
	|	MINUSEQ {: RESULT = Assignment.Operator.MINUSEQ; :}
	|	LSHIFTEQ {: RESULT = Assignment.Operator.LSHIFTEQ; :}
	|	RSHIFTEQ {: RESULT = Assignment.Operator.RSHIFTEQ; :}
	|	URSHIFTEQ {: RESULT = Assignment.Operator.URSHIFTEQ; :}
	|	ANDEQ {: RESULT = Assignment.Operator.ANDEQ; :}
	|	XOREQ {: RESULT = Assignment.Operator.XOREQ; :}
	|	OREQ {: RESULT = Assignment.Operator.OREQ; :}
	;
expression_opt ::= {: RESULT = new Expression(); :}
	|	expression:e {: RESULT = e; :}
	;
expression ::=	assignment_expression:a {: RESULT = a; :}
	;
constant_expression ::=
		expression:e {: RESULT = e; :}
	;
